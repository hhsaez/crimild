# Bundle an app for OSX
# Inputs:
# - CRIMILD_APP_NAME (required)
# - CRIMILD_APP_TARGET_PROPERTIES (optional)
MACRO( CRIMILD_BUNDLE_APP )

	SET( CRIMILD_APP_TARGET_PROPERTIES
		 ${CRIMILD_APP_TARGET_PROPERTIES}
  		 MACOSX_BUNDLE TRUE
  		 MACOSX_FRAMEWORK_IDENTIFIER com.crimild.${CRIMILD_APP_NAME}
	)

	SET( CRIMILD_APP_BINARY_DIR "${CMAKE_CURRENT_BINARY_DIR}" )
	SET( CRIMILD_APP_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}" )
	SET( CRIMILD_APP_BUNDLE_DIR "${CRIMILD_APP_BINARY_DIR}/${CRIMILD_APP_NAME}.app" )

	SET( CRIMILD_APP_FRAMEWORKS_DIR "${CRIMILD_APP_BINARY_DIR}/${CRIMILD_APP_NAME}.app/Contents/Frameworks" )
	SET( CRIMILD_APP_RESOURCES_DIR "${CRIMILD_APP_BINARY_DIR}/${CRIMILD_APP_NAME}.app/Contents/Resources" )

	IF ( EXISTS ${CRIMILD_APP_SOURCE_DIR}/assets )
	   ADD_CUSTOM_COMMAND(
			TARGET ${CRIMILD_APP_NAME}
			POST_BUILD
			COMMAND mkdir ARGS -p ${CRIMILD_APP_RESOURCES_DIR}
			COMMENT "Creating bundle Resources directory"
			VERBATIM
	   )
	   ADD_CUSTOM_COMMAND(
		TARGET ${CRIMILD_APP_NAME}
		POST_BUILD
		COMMAND cp ARGS -r ${CRIMILD_APP_SOURCE_DIR}/assets ${CRIMILD_APP_RESOURCES_DIR}
		COMMENT "Copying assets to bundle"
		VERBATIM
	   )
	ENDIF ()

	ADD_CUSTOM_COMMAND(
		TARGET ${CRIMILD_APP_NAME}
		POST_BUILD
		COMMAND mkdir ARGS -p ${CRIMILD_APP_FRAMEWORKS_DIR}
		COMMENT "Creating bundle Frameworks directory"
		VERBATIM
	)

	ADD_CUSTOM_COMMAND(
		TARGET ${CRIMILD_APP_NAME}
		POST_BUILD
		COMMAND ${CMAKE_COMMAND} ARGS -E tar -x ${CRIMILD_SOURCE_DIR}/third-party/SDL2-osx.zip
		WORKING_DIRECTORY ${CRIMILD_APP_FRAMEWORKS_DIR}
		COMMENT "Copying SDL2 Framework"
		VERBATIM
	)

	ADD_CUSTOM_COMMAND(
		TARGET ${CRIMILD_APP_NAME}
		POST_BUILD
		COMMAND ${CMAKE_COMMAND} ARGS -E tar -x ${CRIMILD_SOURCE_DIR}/third-party/SDL2_mixer-osx.zip
		WORKING_DIRECTORY ${CRIMILD_APP_FRAMEWORKS_DIR}
		COMMENT "Copying SDL2_mixer Framework"
		VERBATIM
	)

	ADD_CUSTOM_COMMAND(
		TARGET ${CRIMILD_APP_NAME}
		POST_BUILD
		COMMAND install_name_tool ARGS -add_rpath @executable_path/../Frameworks ${CRIMILD_APP_BUNDLE_DIR}/Contents/MacOS/${CRIMILD_APP_NAME}
		COMMENT "Configuring search path for frameworks"
		VERBATIM
	)

ENDMACRO ()
